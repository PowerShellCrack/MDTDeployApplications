<job id="Install-Firefox">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-Firefox.wsf
' //
' // Purpose: This will install Mozilla Firefox
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-Firefox.wsf [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal 
'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sInstalledPath
	Dim sPrefix
	Dim sLogFolder,sLogName,sLogPath
	
	'// Apply Architecture arguments:
	'// If no argument provided check for MDT/SCCM variable
	'// If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = LCase(oUtility.Arguments("arch"))
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = LCase(oEnvironment.Item("Architecture"))
	Else
		sArch = "x86"
	End If
	
	' check if MDT variable exists
	If oUtility.Arguments.Exists("prefix") Then
		sPrefix = LCase(oUtility.Arguments("prefix"))
	ElseIf oEnvironment.Exists("Prefix") Then
		sPrefix = oEnvironment.Item("Prefix")
	End if
	
	'// Variables: 
	'// Change if needed
	sVersion = "67.0.3"
	
	If sArch = "x64" then
		sFile = "Firefox Setup " & sVersion & " (x64).exe"
		sInstalledPath = "C:\Program Files\Mozilla Firefox"
	Else
		sFile = "Firefox Setup " & sVersion & ".exe"
		sInstalledPath = "C:\Program Files (x86)\Mozilla Firefox"
	End If
	
	sInstallName = "Mozilla Firefox"
	
	sInstallerPath = oUtility.ScriptDir & "\Source\" & sVersion & "\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Exists("_SMSTSLogPath") Then 
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	Else
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ -ms /INI=""" & oUtility.ScriptDir & "\Configs\firefox.ini""""")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo

		'Copy deployment.properties
		If oFSO.FileExists(oUtility.ScriptDir & "\Configs\mozilla." & LCase(sPrefix) & ".cfg") then
			oFSO.CopyFile (oUtility.ScriptDir & "\Configs\mozilla." & LCase(sPrefix) & ".cfg"), (sInstalledPath & "\mozilla.cfg"), True
			oFSO.CopyFile (oUtility.ScriptDir & "\Configs\override.ini"), (sInstalledPath & "\browser\override.ini"), True
			oFSO.CopyFile (oUtility.ScriptDir & "\Configs\local-settings.js"), (sInstalledPath & "\defaults\pref\local-settings.js"), True
			oLogging.CreateEntry "Configurations files were copied: " & (oUtility.ScriptDir & "\Configs\mozilla."& LCase(sPrefix) & ".cfg"), LogTypeInfo
		Else
			oLogging.CreateEntry oUtility.ScriptDir & "\Configs\mozilla." & LCase(sPrefix) & ".cfg" & " was not found. no configurations was set ", LogTypeInfo
		End If

		
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

</script>
</job>























